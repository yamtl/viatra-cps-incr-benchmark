/*******************************************************************************
 * Copyright (c) 2014-2016 IncQuery Labs Ltd.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     Akos Horvath, Abel Hegedus, Marton Bur, Zoltan Ujhelyi - initial API and implementation
 *******************************************************************************/
package org.eclipse.viatra.examples.cps.xform.m2m.incr.qrt.queries

import "http://org.eclipse.viatra/model/cps"
import "http://org.eclipse.viatra/model/deployment"
import "http://org.eclipse.viatra/model/cps-traceability"

import java org.eclipse.viatra.examples.cps.xform.m2m.util.SignalUtil

pattern mappedCPS(cps2dep : CPSToDeployment, cps : CyberPhysicalSystem, deployment : Deployment) {
	CPSToDeployment.cps(cps2dep, cps);
	CPSToDeployment.deployment(cps2dep, deployment);
}

pattern cps2depTrace(cps2dep : CPSToDeployment, trace : CPS2DeploymentTrace, cpsElement : Identifiable, depElement : DeploymentElement) {
	CPSToDeployment.traces(cps2dep, trace);
	CPS2DeploymentTrace.cpsElements(trace, cpsElement);
	CPS2DeploymentTrace.deploymentElements(trace, depElement);
}

pattern hostInstance(hostInstance : HostInstance) {
	HostInstance(hostInstance);
}

pattern applicationInstance(appType : ApplicationType, appInstance : ApplicationInstance){
	HostInstance.applications(_, appInstance);
	ApplicationType.instances(appType, appInstance);
}

pattern stateMachine(appInstance : ApplicationInstance, stateMachine : StateMachine) {
	find applicationInstance(appType, appInstance);
	ApplicationType.behavior(appType, stateMachine);
}

pattern state(appInstance : ApplicationInstance, stateMachine : StateMachine, state : State) {
	find stateMachine(appInstance, stateMachine);
	StateMachine.states(stateMachine, state);
}

pattern transition(appInstance : ApplicationInstance, srcState : State, targetState : State, transition : Transition) {
	find state(appInstance, stateMachine, srcState);
	State.outgoingTransitions(srcState, transition);
	Transition.targetState(transition, targetState);
	StateMachine.states(stateMachine, targetState);
}

pattern depBehaviorsStateAndTransitions(depBehavior : DeploymentBehavior, depState : BehaviorState, depTransition : BehaviorTransition) {
	DeploymentBehavior.states(depBehavior, depState);
	DeploymentBehavior.transitions(depBehavior, depTransition);
}

pattern allocatedDeploymentApplication(depHost : DeploymentHost, depApp : DeploymentApplication) {
	DeploymentHost.applications(depHost, depApp);
}

pattern applicationInstanceWithHost(appType : ApplicationType, appInstance : ApplicationInstance, host : HostInstance) {
	find applicationInstance(appType, appInstance);
	ApplicationInstance.allocatedTo(appInstance, host);
}

pattern sendTransitionAppSignal(transition : Transition, app : java String, appType : ApplicationType, signal : java String) {
 	ApplicationType.behavior(appType, stateMachine);
	StateMachine.states(stateMachine, source);
	State.outgoingTransitions(source, transition);
 	Transition.action(transition, action);
 	app == eval(SignalUtil.getAppId(action));
 	signal == eval(SignalUtil.getSignalId(action));
}

pattern waitTransitionAppSignal(transition : Transition, appType : ApplicationType, signal : java String) {
 	ApplicationType.behavior(appType, stateMachine);
	StateMachine.states(stateMachine, source);
	State.outgoingTransitions(source, transition);
 	Transition.action(transition, action);
 	check(
 		SignalUtil.isWait(action)
 	);
 	signal == eval(SignalUtil.getSignalId(action));
}

pattern hostFromTransition(appType : ApplicationType, appInstance : ApplicationInstance, transition : Transition, hostInstance : HostInstance) {
	find appInstanceTransition(appInstance, transition);
	find applicationInstanceWithHost(appType, appInstance, hostInstance);
}

private pattern triggerPairWithoutReachability(sourceHostInstance : HostInstance, targetHostInstance : HostInstance, appInstanceTrigger : ApplicationInstance, appInstanceTarget : ApplicationInstance, cpsTrigger : Transition, cpsTarget : Transition) {
	find sendTransitionAppSignal(cpsTrigger, appId, triggerApp, signal);
	find waitTransitionAppSignal(cpsTarget, waitApp, signal);
	ApplicationType.identifier(waitApp, appId);
	
	find hostFromTransition(triggerApp, appInstanceTrigger, cpsTrigger, sourceHostInstance);	
	find hostFromTransition(waitApp, appInstanceTarget, cpsTarget, targetHostInstance);
}

pattern triggerPair(sourceHostInstance : HostInstance, targetHostInstance : HostInstance, appInstanceTrigger : ApplicationInstance, appInstanceTarget : ApplicationInstance, cpsTrigger : Transition, cpsTarget : Transition) {
	find triggerPairWithoutReachability(sourceHostInstance, targetHostInstance, appInstanceTrigger, appInstanceTarget, cpsTrigger, cpsTarget);		
	find reachableHosts(sourceHostInstance, targetHostInstance);
}

pattern appInstanceTransition(appInstance : ApplicationInstance, transition : Transition) {
	find applicationInstance(appType, appInstance);
	ApplicationType.behavior.states.outgoingTransitions(appType, transition);
}

pattern communicatingAppInstances(sourceAppInstance : ApplicationInstance, targetAppInstance : ApplicationInstance) {
	find applicationInstanceWithHost(_, sourceAppInstance, sourceHostInstance);
	find applicationInstanceWithHost(_, targetAppInstance, targetHostInstance);
	find reachableHosts(sourceHostInstance, targetHostInstance);
}

pattern hostCommunication(fromHost : HostInstance, toHost : HostInstance) {
	HostInstance.communicateWith(fromHost, toHost);
}

pattern reachableHosts(source : HostInstance, target : HostInstance) {
	find hostCommunication(source, target);
} or {
	HostInstance(source);
	HostInstance(target);
	source == target;
}